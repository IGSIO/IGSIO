#---------------------------------------------------
set(IGSIO_DEPENDENCIES)

include(ExternalProject)

#---------------------------------------------------
# Codecs
if(IGSIO_USE_VP9)
  include(${CMAKE_CURRENT_LIST_DIR}/External_VP9.cmake)
  if(NOT VP9_FOUND)
    list(APPEND IGSIO_DEPENDENCIES VP9)
  endif()
endif()

#---------------------------------------------------
# libwebm
if(IGSIO_SEQUENCEIO_ENABLE_MKV)
  include(${CMAKE_CURRENT_LIST_DIR}/External_libwebm.cmake)
  if(NOT libwebm_FOUND)
    list(APPEND IGSIO_DEPENDENCIES libwebm)
  endif()
endif()

#---------------------------------------------------
# vtkAddon
if (NOT IGSIO_USE_3DSlicer)
  include(${CMAKE_CURRENT_LIST_DIR}/External_vtkAddon.cmake)
  if(NOT vtkAddon_FOUND)
    list(APPEND IGSIO_DEPENDENCIES vtkAddon)
  endif()
endif()

set(BUILD_OPTIONS
  -DIGSIO_SUPERBUILD:BOOL=OFF
  -DIGSIO_BUILD_SEQUENCEIO:BOOL=${IGSIO_BUILD_SEQUENCEIO}
  -DIGSIO_BUILD_VOLUMERECONSTRUCTION=${IGSIO_BUILD_VOLUMERECONSTRUCTION}
  -DIGSIO_SEQUENCEIO_ENABLE_MKV:BOOL=${IGSIO_SEQUENCEIO_ENABLE_MKV}
  -DIGSIO_USE_SYSTEM_ZLIB:BOOL=${IGSIO_USE_SYSTEM_ZLIB}
  -DIGSIO_USE_3DSlicer:BOOL=${IGSIO_USE_3DSlicer}
  -DIGSIO_USE_VP9:BOOL=${IGSIO_USE_VP9}
  -DIGSIO_USE_GPU:BOOL=${IGSIO_USE_GPU}
  -DVTK_DIR:PATH=${VTK_DIR}
  -DITK_DIR:PATH=${ITK_DIR}
  -DQt5_DIR:PATH=${Qt5_DIR}
  -DVP9_DIR:PATH=${IGSIO_VP9_DIR}
  -DvtkAddon_DIR:PATH=${IGSIO_vtkAddon_DIR}
  -Dlibwebm_DIR:PATH=${IGSIO_libwebm_DIR}
  -DSlicer_DIR:PATH=${Slicer_DIR}
  -DCMAKE_CXX_STANDARD_REQUIRED:BOOL=${CMAKE_CXX_STANDARD_REQUIRED}
  -DCMAKE_MACOSX_RPATH:BOOL=${CMAKE_MACOSX_RPATH}
  )

if (CMAKE_CXX_STANDARD)
  list(APPEND BUILD_OPTIONS
    -DCMAKE_CXX_STANDARD:STRING=${CMAKE_CXX_STANDARD}
    )
endif()

if (APPLE)
  list(APPEND BUILD_OPTIONS
    -DCMAKE_OSX_ARCHITECTURES:STRING=${CMAKE_OSX_ARCHITECTURES}
    -DCMAKE_OSX_DEPLOYMENT_TARGET:STRING=${CMAKE_OSX_DEPLOYMENT_TARGET}
    -DCMAKE_OSX_SYSROOT:STRING=${CMAKE_OSX_SYSROOT}
    )
endif()

ExternalProject_Add( inner-build
  PREFIX "${CMAKE_BINARY_DIR}/IGSIO-prefix"
  SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}"
  BINARY_DIR "${CMAKE_BINARY_DIR}/inner-build"
  #--Download step--------------
  GIT_REPOSITORY ""
  GIT_TAG ""
  #--Configure step-------------
  CMAKE_ARGS
    ${PLATFORM_SPECIFIC_ARGS}
    # Compiler settings
    -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
    -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
    -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    # Output directories
    -DCMAKE_LIBRARY_OUTPUT_DIRECTORY:PATH=${CMAKE_LIBRARY_OUTPUT_DIRECTORY}
    -DCMAKE_RUNTIME_OUTPUT_DIRECTORY:PATH=${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
    -DCMAKE_ARCHIVE_OUTPUT_DIRECTORY:PATH=${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}
    # Install directories
    -DIGSIO_INSTALL_BIN_DIR:PATH=${IGSIO_INSTALL_BIN_DIR}
    -DIGSIO_INSTALL_LIB_DIR:PATH=${IGSIO_INSTALL_LIB_DIR}
    -DIGSIO_INSTALL_DATA_DIR:PATH=${IGSIO_INSTALL_DATA_DIR}
    # Options
    -DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DBUILD_TESTING:BOOL=${BUILD_TESTING}
    -DBUILDNAME=${BUILDNAME}
    ${BUILD_OPTIONS}

  #--Build step-----------------
  BUILD_ALWAYS 1
  #--Install step-----------------
  INSTALL_COMMAND ""
  DEPENDS ${IGSIO_DEPENDENCIES}
  )
